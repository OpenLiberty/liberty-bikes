apply plugin: 'liberty'
apply plugin: 'war'

sourceCompatibility = 1.8

eclipse {
    project.natures 'org.eclipse.buildship.core.gradleprojectnature',
                    'org.eclipse.wst.jsdt.core.jsNature'
}

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'net.wasdev.wlp.gradle.plugins:liberty-gradle-plugin:2.0.1'
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compileOnly 'javax:javaee-api:7.0'
    libertyRuntime 'io.openliberty:openliberty-runtime:17.0.0.3'
}

ext {
	httpPort = 8080
	httpsPort = 8443
}

liberty {
	server {
		name = 'libertybikes.game'
		dropins = [war]
		bootstrapProperties = ['httpPort': httpPort, 'httpsPort': httpsPort]
	}
}

libertyStart.dependsOn 'libertyStop'
libertyRun.dependsOn 'libertyStop'
libertyStart.doLast {
	println "Application available at: http://localhost:${httpPort}/"
}

// Some convenience tasks
task run   { dependsOn 'libertyRun' }
task start { dependsOn 'libertyStart' }
task stop  { dependsOn 'libertyStop' }
task open {
    group = 'liberty'
    doLast {
        java.awt.Desktop.desktop.browse "http://localhost:${httpPort}/".toURI()
    }
}
